programa eje2
procesos
  proceso juntarFlores (ES cantF:numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cantF:= cantF+1
  fin
  proceso juntarPapeles 
  comenzar
    mientras (HayPapelEnLaEsquina)
      tomarPapel
  fin
  proceso depositarFlores (E cantF:numero)
  comenzar
    repetir cantF
      depositarFlor
  fin
  proceso procesarEsquina (E cantF:numero)
  variables
    avAct:numero
    caAct:numero
  comenzar
    avAct:= PosAv
    caAct:= PosCa
    BloquearEsquina(50,50)
    Pos(50,50)
    depositarFlores(cantF)
    Pos(avAct,caAct)
    LiberarEsquina(50,50)
  fin
  proceso hacerEtapa 
  variables
    cantF:numero
  comenzar
    cantF:=0
    repetir 9
      juntarPapeles
      juntarFlores(cantF)
      mover
    juntarFlores(cantF) {procesamos la ultima esquina}
    si (cantF > 0)
      procesarEsquina(cantF) {esquina 50,50}
  fin
  proceso hacerLasEtapas 
  variables
    puedoSeguir:boolean
  comenzar
    derecha
    puedoSeguir:=V
    {repetir 10  <--- Preguntar si va repetir o mientras}
    mientras (puedoSeguir = V) 
      hacerEtapa
      EnviarMensaje(V,jefe) {le avisa al jefe que termino}
      RecibirMensaje(puedoSeguir,jefe) {espera para ver si sigue o termina.}
    EnviarMensaje(V,jefe){para que avise que termino la ultima de las ultimas etapas}
  fin
  proceso asignarIds 
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
  fin
  proceso esperarQueTerminenEtapa
  variables
    termine:boolean
  comenzar
    repetir 3
      RecibirMensaje(termine,*)
  fin  
  proceso avisarQueContinuenEtapas
  comenzar
    EnviarMensaje(V,r1)
    EnviarMensaje(V,r2)
    EnviarMensaje(V,r3)
  fin
  proceso avisarQueTerminenEtapas
  comenzar
    EnviarMensaje(F,r1)
    EnviarMensaje(F,r2)
    EnviarMensaje(F,r3)
  fin
  proceso habilitarEtapas
  comenzar
    repetir 10 {cantidad de etapas}
      esperarQueTerminenEtapa
      avisarQueContinuenEtapas
    esperarQueTerminenEtapa
    avisarQueTerminenEtapas
  fin
  proceso procesarEsquinaJefe
  variables
    cantF:numero
    avIni:numero
    caIni:numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    cantF:=0
  {no es necesario bloquear la esquina, porque en teoria, ya terminaron todos los robots para que el jefe pueda acceder a esta esquina}
    Pos(50,50)
    juntarFlores(cantF)
    Pos(avIni,caIni)
    Informar('La_cantidad_de_flores_totales_son',cantF)
  fin
areas
  areaR1: AreaP(1,1,100,1)
  areaR2: AreaP(1,2,100,2)
  areaR3: AreaP(1,3,100,3)
  areaJefe: AreaP(1,4,1,4)
  esquinaCompartida: AreaC(50,50,50,50)
robots
  robot rRecolector
  variables
    id:numero
  comenzar
    RecibirMensaje(id,jefe)
    hacerLasEtapas
  fin
  robot rJefe
  comenzar
    asignarIds
    habilitarEtapas
    procesarEsquinaJefe
  fin
variables
  r1: rRecolector
  r2: rRecolector
  r3: rRecolector
  jefe: rJefe
comenzar
  {areas personales para cada robot}
  AsignarArea(r1,areaR1)
  AsignarArea(r2,areaR2)
  AsignarArea(r3,areaR3)
  AsignarArea(jefe,areaJefe)
  {esquina compartida para todos los robots}
  AsignarArea(jefe,esquinaCompartida)
  AsignarArea(r1,esquinaCompartida)
  AsignarArea(r2,esquinaCompartida)
  AsignarArea(r3,esquinaCompartida)
  {iniciamos cada robot}
  Iniciar(jefe,1,4)
  Iniciar(r1,1,1)
  Iniciar(r2,1,2)
  Iniciar(r3,1,3)
fin
