programa eje5
procesos
  proceso depositarPapeles (E cantP:numero)
  comenzar
    repetir cantP
      depositarPapel
  fin
  proceso dejarPapelesAleatorios(E avIni:numero; E caIni:numero; E papelesRandom:numero)
  comenzar
    BloquearEsquina(100,1)
    Pos(100,1)
    depositarPapeles(papelesRandom)
    Pos(avIni,caIni)
    LiberarEsquina(100,1)
  fin
  proceso levantarDeAUnPapel (E avIni:numero; E caIni:numero; E cantPapeles:numero)
  comenzar
    repetir cantPapeles
      BloquearEsquina(100,1)
      Pos(100,1)
      tomarPapel
      Pos(avIni,caIni)
      LiberarEsquina(100,1)
      depositarPapel
  fin
  proceso hacerIntentosCliente (E id:numero)
  variables
    cantPapeles:numero
    avIni:numero
    caIni:numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    repetir 4 {cantidad de intentos}
      EnviarMensaje(id,servidor)
      RecibirMensaje(cantPapeles,servidor)
      levantarDeAUnPapel(avIni,caIni,cantPapeles)
  fin
  proceso avisarAlCliente (E idCliente:numero; E cantDeposito:numero)
  comenzar
    si (idCliente = 1)
      EnviarMensaje(cantDeposito,cliente1)
    sino
      si (idCliente = 2)
        EnviarMensaje(cantDeposito,cliente2)
      sino
        si (idCliente = 3)
          EnviarMensaje(cantDeposito,cliente3)
  fin 
  proceso entregarPapeles
  variables
    idCliente:numero
    avIni:numero
    caIni:numero
    papelesRandom:numero
  comenzar
    avIni:= PosAv
    caIni:= PosCa
    repetir 12 {4 intentos cada uno, 3 robots = 12 intentos totales}
      RecibirMensaje(idCliente,*)
      Random(papelesRandom,1,5)
      dejarPapelesAleatorios(avIni,caIni,papelesRandom)
      avisarAlCliente(idCliente,papelesRandom)
  fin 
  proceso asignarIDs
  comenzar
    EnviarMensaje(1,cliente1)
    EnviarMensaje(2,cliente2)
    EnviarMensaje(3,cliente3)
  fin
areas
  esquinaCompartida: AreaC(100,1,100,1)
  esqCliente1: AreaP(10,1,10,1)
  esqCliente2: AreaP(11,1,11,1)
  esqCliente3: AreaP(12,1,12,1)
  esqServidor: AreaP(13,1,13,1)
robots
  robot rCliente
  variables
    id:numero
  comenzar
    RecibirMensaje(id,servidor)
    hacerIntentosCliente(id)
  fin
  robot rServidor
  comenzar
    asignarIDs
    entregarPapeles
  fin
variables
  cliente1: rCliente
  cliente2: rCliente
  cliente3: rCliente
  servidor: rServidor
comenzar
  {-- asigno esquinas compartidas --}
  AsignarArea(cliente1,esquinaCompartida)
  AsignarArea(cliente2,esquinaCompartida)
  AsignarArea(cliente3,esquinaCompartida)
  AsignarArea(servidor,esquinaCompartida)
  {-- asigno esquinas privadas --}
  AsignarArea(cliente1,esqCliente1)
  AsignarArea(cliente2,esqCliente2)
  AsignarArea(cliente3,esqCliente3)
  AsignarArea(servidor,esqServidor)
  {-- iniciamos en las esquinas --}
  Iniciar(cliente1,10,1)
  Iniciar(cliente2,11,1)
  Iniciar(cliente3,12,1)
  Iniciar(servidor,13,1)
fin